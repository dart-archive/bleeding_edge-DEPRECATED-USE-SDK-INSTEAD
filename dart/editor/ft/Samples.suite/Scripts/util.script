universal platform
universal initialized

to init
	put (@universal initialized is "initialized") into wasInitialized
	if wasInitialized then
		return
	end if
	put the SearchRectangle into origRect
	set the SearchRectangle to ((0,0),(100,100))
	if ImageFound("linux menu icon") then
		put "linux" into universal platform
	else if ImageFound("mac menu icon") then
		put "macosx" into universal platform
	else
		put "windows" into universal platform
	end if
	set the SearchRectangle to origRect
	if RemoteScreenSize() <> (1920,1200) then
		throw "Screen size must be 1920x1200"
	end if
	Set the ImageDoctor to Auto
	put "initialized" into universal initialized
end init

to isLinux
	put (@universal platform is "linux") into result
	return @result
end isLinux

to isMacOSX
	put (@universal platform is "macosx") into result
	return @result
end isMacOSX

to isWindows
	put (@universal platform is "windows") into result
	return @result
end isWindows

to activateEditor
	(*selectMenuItem "Navigate", "Activate Editor"*)
	if isMacOSX() then
		typetext commandKey,F12
	else
		typetext F12
	end if
end activateEditor

to openWelcomePage
	selectMenuItem "Tools", "Welcome Page"
end openWelcomePage

to nextEditor count
	(* Typetext wasn't working correctly on linux, so now using keydown/keyup instead  *)
	repeat count times
		if isMacOSX()
			keydown commandKey,F6
			keyup commandKey,F6
		else
			keydown controlKey,F6
			keyup controlKey,F6
		end if
		(*doKeyCmd F6*)
		Wait 2
	end repeat
end NextEditor

to checkProblems
	put the SearchRectangle into origRect
	set the SearchRectangle to ((0,800),(1920,1200))
	Wait 2
	ClIck (600,500)
	Wait 2
	Click "problems tab"
	WaitFor 1,"empty problems"
	set the SearchRectangle to origRect
end checkProblems

(* Assumes Chromium is the active proecss. *)
to exitChromium
	if isMacOSX()
		(* Can fail if Chromium window is under Chromium menu causing window edge *)
		(* to bleed thru menu due to alpha channel.  Re-position Chromium! *)
		selectMenuItem "Chromium", "Quit Chromium"
	else
		TypeText altKey,F4
	end if
end exitChromium

(* Delete the output tab. Only valid if all projects have been deleted. *)
to deleteOutput
	put the SearchRectangle into origRect
	set the SearchRectangle to ((0,800),(1920,1200))
	put 105 into deltaX
	if isMacOSX() then
		put 85 into deltaX
	end if
	put ImageLocation("problems tab") + (deltaX,0) into outLoc
	Click outLoc
	Wait 0.2
	RightClick outLoc
	Wait 0.2
	repeat while ImageFound("problems unselected")
		put ImageFound(Text:"Close",CaseSensitive:"yes",ValidCharacters:"*") into out
		if out
			Click (Text:"Close",CaseSensitive:"yes",ValidCharacters:"*")
		end if
		Click outLoc
		Wait 0.2
		RightClick outLoc
	end repeat
	Click outLoc - (10,10)
	set the SearchRectangle to origRect
end deleteOutput

(* Delete the project identified by the given image. *)
to deleteProject project
	set the SearchRectangle to ((0,75),(300,1200))
	Click "dart sdk"
	Wait 0.2
	put ImageLocation(project) into loc
	Click loc
	Wait 0.2
	RightClick loc (* select project and show context menu *)
	put mouselocation() - (15,0) into UL
	put UL + (500,500) into BR
	set the SearchRectangle to (UL,BR)
	Wait 0.2
	Click(Text:"Delete",CaseSensitive:"Yes",ValidCharacters:"*") (* delete it *)
	set the SearchRectangle to ()
	WaitFor 1, "info icon" (* wait for the Delete Resources confirmation *)
	put ImageRectangle("info icon") into loc
	put the bottomLeft of loc into TL
	put TL + (250,75) into BR
	set the SearchRectangle to (TL,BR)
	Click(Text:"Delete project contents",ValidCharacters:"*",dpi:94) (* select the option to clear disk *)
	set the SearchRectangle to ()
	Wait 0.2
	newline
end deleteProject

(* Select the menuItem from the menu named menuName on the menu bar *)
to selectMenuItem with menuName, menuItem
	put the SearchRectangle into origRect
	set the SearchRectangle to ((0,0),(1920,200))
	put ImageRectangle(Text:menuName,CaseSensitive:"yes",ValidCharacters:"*",IgnoreSpaces:"yes",dpi:94) into labelRect
	Click center(labelRect)
	put topLeft(labelRect)-(10,0) into UL
	put UL + (500,500) into BR
	set the SearchRectangle to (UL,BR)
	Wait 0.1
	moveTo topLeft(labelRect) (* encourage mac vnc to update *)
	Wait 0.1
	moveTo bottomRight(labelRect)
	Wait 0.1
	Click(Text:menuItem,CaseSensitive:"yes",ValidCharacters:"*",IgnoreSpaces:"yes",dpi:94)
	set the SearchRectangle to origRect
end selectMenuItem

(* Select the menuItem from the context menu available by right-clicking at contextLocator text *)
to selectContextMenuItem with contextLocator, menuItem
	selectContextImageMenuItem (Text:contextLocator,CaseSensitive:"yes",ValidCharacters:"*",IgnoreSpaces:"yes",dpi:94), menuItem
end selectContextMenuItem

(* Select the menuItem from the context menu available by right-clicking at contextLocator *)
to selectContextImageMenuItem with contextLocator, menuItem
	put the SearchRectangle into origRect
	put ImageLocation(contextLocator) into imageLocation
	Click imageLocation
	RightClick imageLocation (*contextLocator*)
	put mouselocation() - (15,0) into UL
	put UL + (500,500) into BR
	set the SearchRectangle to (UL,BR)
	Wait 0.1
	Click(Text:menuItem,CaseSensitive:"yes",ValidCharacters:"*",IgnoreSpaces:"yes",dpi:94)
	set the SearchRectangle to origRect
end selectContextImageMenuItem

(* Enter term (slowly) and wait a bit *)
to typeSlowly with term
	SetOption KeyDownDelay, 0.002
	TypeText term
	SetOption KeyDownDelay, 0.001
	Wait 0.5
end typeSlowly

(* Stop debugging and close the debugger, if it is open *)
to closeDebugger
	put ImageFound("debugger tab") into bug
	if bug
		Put ImageFound("stop debug button") into res
		if res then
			Click "stop debug button"
		end if
		Click "debugger tab"
	end if
end closeDebugger

(* Start the editor, typically only run at the beginning of a script *)
to startDartEditor
	init()
	set the SearchRectangle to ((0,0),(200,200))
	DoubleClick "StartDartEditor"
	WaitFor 40,"MaximizeWindowIcon"
	Click "MaximizeWindowIcon"
	set the SearchRectangle to ()
	if isMacOSX()
		SetOption ForceScreenRefresh, true
	end if
end startDartEditor

(* Exit the editor *)
to stopDartEditor
	if isMacOSX()
		TypeText commandKey,q
	else
		TypeText altKey,F4
	end if
end stopDartEditor

to doKeyCmd key
	if isMacOSX()
		TypeText commandKey,key
	else
		TypeText controlKey,key
	end if
end doKeyCmd

to doShiftKeyCmd key
	if isMacOSX()
		TypeText commandKey,shiftKey,key
	else
		TypeText controlKey,shiftKey,key
	end if
end doShiftKeyCmd

to closeTab
	doKeyCmd("w")
end closeTab

to closeAllTabs
	doShiftKeyCmd("w")
end closeAllTabs

to saveFile
	doKeyCmd("s")
end saveFile

to openFileDialog
	SelectMenuItem "File", "Existing Folder"
	Wait 0.5
end openFileDialog

to openFind
	doKeyCmd("f")
end openFind

to runApp
	doKeyCmd("r")
end runApp

to selectAll
	doKeyCmd("a")
end selectAll

to copyText
	doKeyCmd("c")
end copyText

to pasteText
	doKeyCmd("v")
end pasteText

to cutText
	doKeyCmd("x")
end cutText

to undo
	doKeyCmd("z")
end undo

to newTab
	doKeyCmd("t")
end newTab

to searchBox
	doKeyCmd("3")
end searchBox

to saveFile
	doKeyCmd("s")
end saveFile

to expandTreeNode
	if isMacOSX() then
		TypeText enter
	else
		TypeText return
	end if
end expandTreeNode

to endOfLine
	if isMacOSX() then
		doKeyCmd(rightArrow)
	else
		TypeText EndKey
	end if
end endOfLine

to quickOutline
	TypeText optionKey,o
end quickOutline

to newline
	if isMacOSX() then
		TypeText enter
	else
		TypeText return
	end if
end newline
